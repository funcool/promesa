= promesa - promise library for clojure and clojurescript
Andrey Antukh, <niwi@niwi.nz>
:toc: left
:!numbered:
:idseparator: -
:idprefix:
:sectlinks:
:source-highlighter: pygments
:pygments-style: friendly


== Introduction

A promise library for Clojure and ClojureScript.

On the JVM paltform _promesa_ is built on top of *completable futures*
(requires jdk>=8). On JS engines it is built on top of the built-in
Promise implementation.


=== Project Maturity

Since _promesa_ is a young project there may be some API breakage.


=== Install

Just include the following lines in your dependency vector on *_project.clj_*:

[source,clojure]
----
[funcool/promesa "4.0.0"]
----

This package requires JDK8 if you are using it on the JVM.


== User guide

=== Introduction

A promise is an abstraction that represents the result of an asynchronous
operation that has the notion of error.

This is a list of all possible states for a promise:

- `resolved`: means that the promise contains a value.
- `rejected`: means that the promise contains an error.
- `pending`: means that the promise does not have value.

The promise can be considered *done* when it is resolved or rejected.


=== Creating a promise

There are several different ways to create a promise instance. Let's
start by creating promise instances from plain values:

[source, clojure]
----
(require '[promesa.core :as p])

;; Create a resolved promise
(p/resoved 1)
;; => #<Promise [~]>

;; Create a rejected promise
(p/rejected (ex-info "error" {}))
;; => #<Promise [~]>
----

Another option is to create an empty promise using the `deferred` function
and provide the value asynchronously using `p/resolve!` and `p/reject!`:

[source, clojure]
----
(defn sleep
  [ms]
  (let [p (p/deferred)]
    (js/setTimeout #(p/resolve! p))
    p))
----

The `deferred` function also accepts plain values and functions as arguments. If
a value is provided, a resolved promise will be returned. If a function is
provided, the function will be called and the return value will be used to
resolve the promise.

[source, clojure]
----
@(p/deferred (constantly 1))
;; => 1
----

If you are familiar with JavaScript, there is a similar approach
(internally called a factory).

.Example creating a promise instance using a factory.
[source, clojure]
----
@(p/promise (fn [resolve reject] (resolve 1)))
;; => 1
----

The factory will be executed synchronously (in the current thread) but
if you want to execute it asynchronously, you can provide an executor:

[source, clojure]
----
(require '[promesa.exec :as exec])

@(p/promise (fn [resolve reject] (resolve 1)) exec/default-executor)
;; => 1
----

Another way to create a promise is to use the `do!` macro. The `do!` macro works
similarly to the factory callback, except that we can return a value directly
instead of calling `resolve`:

[source, clojure]
----
(p/do!
  (let [a (rand-int 10)
        b (rand-int 10)]
    (+ a b)))
----

The `do!` expression work similarly to clojure's `do` block, so you can provide
any expression, but only the last one will be returned. That expression can be a
plain value or an other promise.

If an exception is raised inside the `do!` block, it will return the
rejected promise instead of re-raising the exception on the stack.

If the `do!` contains more than one expression, each expression will be treated
as a promise expression and will be executed sequentially, each awaiting the
resolution of the prior expression.

For example, this `do!` macro:

[source, clojure]
----
(p/do! (expr1nn)
       (expr2)
       (expr3))
----

Is roughtly equivalent to:

[source, clojure]
----
(p/let [_ (expr1)
        _ (expr2)]
  (expr3))
----

Finally, **promesa** exposes a `future` macro very similar to the
`clojure.core/future`:

[source, clojure]
----
@(p/future (some-complex-task))
;; => "result-of-complex-task"
----

One difference from `clojure.core/future` is that if the return value of the
future expression is itself a promise instance, then it will await and unwrap
the inner promise:

[source, clojure]
----
@(p/future (p/future (p/future 1)))
;; => 1
----


=== Promise Chaining

The most common way to apply a function to a promise (or in other words, to chain
its execution) is using the well-known `map` function:

[source, clojure]
----
(def result
  (->> (p/resolved 1)
       (p/map inc)))

@result
;; => 2
----

For people coming from the JS world, there is also the `then` function that
works in very similar way to `map` with the exception that the parameters have
different order and flatten automatically.

[source, clojure]
----
@(-> (p/resolved 1)
     (p/then inc))
;; => 2

;; flatten result
@(-> (p/resolved 1)
     (p/then (fn [x] (p/resolved (inc x)))))
;; => 2
----

If you want to apply multiple functions instead of using multiple
`then` or `map`s, you can use the `chain` function:

[source, clojure]
----
(def result
  (-> (p/resolved 1)
      (p/chain inc inc inc)))

@result
;; => 4
----

The `chain` function uses `then` behind the scenes, so the same rules about
flattening applies.

NOTE: if you know that your chained functions do not return
promises, you can use `chain'` instead for better performance.


=== Promise Composition

==== `let`

The _promesa_ library comes with convenient syntactic-sugar that allows
you to create a composition that looks like synchronous code while
using the clojure's familiar `let` syntax:

[source, clojure]
----
(require '[promesa.exec :as exec])

(defn sleep-promise
  [wait]
  (p/promise (fn [resolve reject]
               (exec/schedule! wait #(resolve wait)))))

(def result
  (p/let [x (sleep-promise 42)
          y (sleep-promise 41)
          z 2]
    (+ x y z)))

@result
;; => 85
----

The `let` macro behaves identically to the `let` with the exception
that it always return a promise.

If an error occurs at any step, the entire composition will be
short-circuited, returning exceptionally resolved promise.

Under the hood, the previous `let` macro evalutes to something like
this:

[source, clojure]
----
(p/then (sleep-promise 42)
        (fn [x] (p/then (sleep-promise 41)
                        (fn [y] (p/then 2 (fn [z]
                                            (p/resolved (do (+ x y z)))))))))
----

==== `all`

In some circumstances you will want wait for completion of several promises at
the same time. To help with that, _promesa_ also provides helpers.

Imagine that you have a collection of promises and you want to wait until all of
them are resolved. This can be done using the `all` combinator:

[source, clojure]
----
(let [p (p/all [(do-some-io)
                (do-some-other-io)])]
  (p/then p (fn [[result1 result2]]
              (do-something-with-results result1 result2))))
----


==== `plet`

The `plet` macro combines syntax of `let` with `all`. It enables a simple
declaration of parallel operations followed by a body expression that
will be executed when all parallel operations have successfully
resolved.

[source, clojure]
----
@(p/plet [a (p/delay 100 1)
          b (p/delay 200 2)
          c (p/delay 120 3)]
   (+ na b c))
;; => result: 6
----

The `plet` macro is just a syntactic sugar on top of `all`. The previous example
can be written using `all` in this manner:

[source, clojure]
----
(p/all [(p/delay 100 1)
        (p/delay 200 2)
        (p/delay 120 3)]
  (fn [[a b c]] (+ a b c)))
----


==== `any`

There are also circumstances where you only want the first successfully resolved
promise. For this case, you can use the `any` combinator:

[source, clojure]
----
(let [p (p/any [(p/delay 100 1)
                (p/delay 200 2)
                (p/delay 120 3)])]
  (p/then p (fn [x]
              (.log js/console "The first one finished: " x))))
----


==== `race`

The `race` function method returns a promise that fulfills or rejects
as soon as one of the promises in an iterable fulfills or rejects,
with the value or reason from that promise:

[source, clojure]
----
@(p/race [(p/delay 100 1)
          (p/delay 110 2)])
;; => 1
----


=== Error handling

One of the advantages of using the promise abstraction is that it natively has a notion
of errors, so you don't need reinvent it. If some computation inside the composed
promise chain/pipeline raises an exception, the pipeline short-circuits and propogates
the exception to the last promise in the chain.

Let see an example:

[source, clojure]
----
(-> (p/rejected (ex-info "error" nil))
    (p/catch (fn [error]
               (.log js/console error))))
----

The `catch` function adds a new handler to the promise chain that will be called
when any of the previous promises in the chain are rejected or an exception is
raised. The `catch` function also returns a promise that will be resolved or
rejected depending on that will happen inside the catch handler.

If you prefer `map`-like parameters order, the `err` function (and `error`
alias) works in same way as `catch` but has parameters ordered like `map`:

[source, clojure]
----
(->> (p/rejected (ex-info "error" nil))
     (p/error (fn [error]
                (.log js/console error))))
----

[NOTE]
====
On the JVM platform the reject value must be an instance of `Throwable`, but on
the JavaScript platform the reject value can be any value.
====


=== Delays and timeouts.

JavaScript, due its single-threaded nature, does not allow you to block or
sleep. But, with promises you can emulate that functionality using `delay` like
so:

[source, clojure]
----
(-> (p/delay 1000 "foobar")
    (p/then (fn [v]
              (println "Received:" v))))

;; After 1 second it will print the message
;; to the console: "Received: foobar"
----

The promise library also offers the ability to add a timeout to async
operations thanks to the `timeout` function:

[source, clojure]
----
(-> (some-async-task)
    (p/timeout 200)
    (p/then #(println "Task finished" %))
    (p/catch #(println "Timeout" %)))
----

In this example, if the async task takes more that 200ms then the promise will
be rejected with a timeout error and then successfully captured with the `catch`
handler.


=== Scheduling Tasks

In addition to the promise abstraction, this library also comes with a
lightweight abstraction for scheduling task to be executed at some
time in future:

.Example using the `schedule` function.
[source, clojure]
----
(require '[promesa.exec :as exec])
(exec/schedule! 1000 (fn []
                       (println "hello world")))
----

This example shows you how you can schedule a function call to be
executed 1 second in the future. It works the same way for both
plaforms (clj and cljs).

The tasks can be cancelled using its return value:

[source, clojure]
----
(def task (exec/schedule! 1000 #(do-stuff)))

(p/cancel! task)
----

== Advanced topics

=== Execution model

NOTE: This section is mainly affects the **JVM**.

Lets take this example as a context:

[source, clojure]
----
@(-> (p/delay 100 1)
     (p/then inc)
     (p/then inc))
;; => 3
----

This will create a promise that will resolve to `1` in 100ms (in a
separated thread); then the first `inc` will be executed (in the same
thread) and then another `inc` is executed (in the same
thread). In total only one thread is involved.

This default execution model is usually preferrable because it don't abuse
task scheduling and leverages function inlining on the JVM.

But it does have drawbacks: this approach will block the thread until
all of the chained callbacks are executed. For small chains this is not a
problem. However, if your chain has a lot of functions and requires a
lot of computation time, this might cause unexpected latency. It may block
other threads in the thread pool from doing other, maybe more important,
tasks.

For such cases, **promesa** exposes an additional arity for provide a
user-defined executor to control where the chained callbacks are executed:

[source, clojure]
----
(require '[promesa.exec :as exec])

@(-> (p/delay 100 1)
     (p/then inc exec/default-executor)
     (p/then inc exec/default-executor))
;; => 3
----

This will schedule a separated task for each chained callback, making
the whole system more responsive because you are no longer executing
big blocking functions; instead you are executing many small tasks.

The `exec/default-executor` is a `ForkJoinPool` instance that is highly
optimized for lots of small tasks.

In some cases you may still want execute all chained functions
together but all together in different executor. In this case you can
use the `deferred` constructor to specify the executor.

@(-> (p/delay 100 1)
     (p/deferred exec/default-executor)
     (p/chain inc inc))


== Developers Guide

=== Contributing

Unlike Clojure and other Clojure contrib libs, this project does not have many restrictions for
contributions. Just open a issue or pull request.


=== Get the Code

_promesa_ is open source and can be found on
link:https://github.com/funcool/promesa[github].

You can clone the public repository with this command:

[source,text]
----
git clone https://github.com/funcool/promesa
----


=== Run tests

To run the tests execute the following:

For the JVM platform:

[source, text]
----
lein test
----

And for JS platform:

[source, text]
----
./scripts/build
node out/tests.js
----

You will need to have nodejs installed on your system.


=== License

_promesa_ is licensed under BSD (2-Clause) license:

----
Copyright (c) 2015-2019 Andrey Antukh <niwi@niwi.nz>

All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

* Redistributions of source code must retain the above copyright notice, this
  list of conditions and the following disclaimer.

* Redistributions in binary form must reproduce the above copyright notice,
  this list of conditions and the following disclaimer in the documentation
  and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
----
